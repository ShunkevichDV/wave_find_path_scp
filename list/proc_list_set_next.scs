//Устанавливает следующий элемент списка _next_element для элемента _current_element
//Поддерживает обработку в тех случаях, когда у _current_element уже есть следующий элемент.

proc_list_set_next
=> nrel_main_idtf:
	[Устанавливает следующий элемент списка _next_element для элемента _current_element] (* <- lang_ru;; *);
	[list set next element] (* <- lang_en;; *);
<- scp_program;
-> rrel_key_sc_element: .._process1;;

proc_list_set_next = [*
.._process1
_<- scp_process;

_-> rrel_1:: rrel_in:: .._current_element;
_-> rrel_2:: rrel_in:: .._next_element;

_<= nrel_decomposition_of_action:: .._actions(*

	_-> rrel_1:: .._proc_list_set_next_operator1 (*
		_<- searchElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: .._current_element;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: .._arc2;;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: .._current_next;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: .._arc4;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: rrel_list_next;;

		_=> nrel_then:: .._proc_list_set_next_operator2;;
		_=> nrel_else:: .._proc_list_set_next_operator3;;
	*);; 

	_-> .._proc_list_set_next_operator2 (*
		_<- eraseEl;;
		_-> rrel_1:: rrel_fixed:: rrel_erase:: rrel_scp_var:: .._arc2;;

		_=> nrel_goto:: .._proc_list_set_next_operator3;;
	*);;

	_-> .._proc_list_set_next_operator3 (*
		_<- genElStr5;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: .._current_element;;
		_-> rrel_2:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var::  _arc2;;
		_-> rrel_3:: rrel_fixed:: rrel_scp_const:: .._next_element;;
		_-> rrel_4:: rrel_assign:: rrel_pos_const_perm:: rrel_scp_var:: .._arc4;;
		_-> rrel_5:: rrel_fixed:: rrel_scp_const:: rrel_list_next;;

		_=> nrel_goto::  .._proc_list_set_next_operator_return;;
	*);;

	_-> .._proc_list_set_next_operator_return (*
		_<- return;;
	*);;			
*);;
*];;
