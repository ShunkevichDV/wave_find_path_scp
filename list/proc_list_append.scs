//Создаёт новый элемент списка со значением _new_element_value и добавляет его в список после элемента _last_element.
//Созданный элемент списка возвращается в _new_element.
proc_list_append
=> nrel_main_idtf:
	[Создаёт новый элемент списка со значением _new_element_value и добавляет его в список после элемента _last_element] (* <- lang_ru;; *);
	[list append] (* <- lang_en;; *);
<- scp_program;
-> rrel_key_sc_element: _process1;;

proc_list_append = [*
_process1 
_<- scp_process;

_-> rrel_1:: rrel_in:: _last_element;					// Элемент списка, после которго будет добавлен новый элемент
_-> rrel_2:: rrel_in:: _new_element_value;			// Значение нового элемента
_-> rrel_3:: rrel_out:: _new_element;				// Созданный новый элемент

_<= nrel_decomposition_of_action:: _... (*

	_-> rrel_1:: _..proc_list_append_operator1 (*
		_<- call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_list_new;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: ...(*
			_-> rrel_1:: rrel_fixed:: rrel_scp_const::  _new_element_value;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_var:: _new_element;;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr;;

		_=> nrel_goto:: _..proc_list_append_operator2;;
	*);;

	_->_..proc_list_append_operator2 (*
		_<-waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr;;

		_=> nrel_goto:: _..proc_list_append_operator3;;
	*);;

	_-> _..proc_list_append_operator3 (*
		_<- call;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_const:: proc_list_set_next;;
		_-> rrel_2:: rrel_fixed:: rrel_scp_const:: _..params(*
			_-> rrel_1:: rrel_fixed:: rrel_scp_const:: _last_element;;
			_-> rrel_2:: rrel_fixed:: rrel_scp_var:: _new_element;;
		*);;
		_-> rrel_3:: rrel_assign:: rrel_scp_var:: _descr;;

		_=> nrel_goto:: _..proc_list_append_operator4;;
	*);;
	
	_->_..proc_list_append_operator4 (*
		_<-waitReturn;;
		_-> rrel_1:: rrel_fixed:: rrel_scp_var:: _descr;;

		_=> nrel_goto:: _..proc_list_append_operator_return;;
	*);;

	_-> _..proc_list_append_operator_return (*
		_<- return;;
	*);;	
*);;
*];;